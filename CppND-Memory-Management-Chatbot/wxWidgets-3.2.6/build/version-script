#
# Notes:
#
# Remove all of the tags below except for the last one when creating new
# development branch.
#
# Once a symbol has been included in a stable release of wxWidgets its version
# cannot be changed.
#
# With each new release a section should be added containing any new symbols,
# for example:
#
#   # public symbols added in release WXU_3.2.2 (please keep in alphabetical order):
#   WXU_3.2.2 {
#       extern "C++"
#           "wxChoice::GetCurrentSelection()";
#       };
#   };
#
# If a symbols should have been added in this way, but is forgotten then it
# cannot be added in the next release. This is because it has already been
# released with the generic branch version due to the final wildcard below,
# and once released its version cannot be changed.

# When adding a new section here, don't forget to modify the version in
# build/bakefiles/version.bkl to indicate that new APIs have been added and
# rebake!

# public symbols added in 3.2.6 (please keep in alphabetical order):
WXU_3.2.6 {
    extern "C++" {
        "AddAvailableCatalog(const wxString&, wxLanguage)";
        "wxEventTableEntry::wxEventTableEntry(const wxEventTableEntry&)";
        "wxSystemSettingsNative::SelectLightDark";
    };
};

# public symbols added in 3.2.3 (please keep in alphabetical order):
WXU_3.2.3 {
    extern "C++" {
        "wxGLCanvasEGL::CreateWaylandSubsurface";
        "wxGLCanvasEGL::DestroyWaylandSubsurface";
        "wxTranslations::AddAvailableCatalog";
        "wxTranslations::GetBestAvailableTranslation";
        "wxUILocale::GetMonthName";
        "wxUILocale::GetWeekDayName";
    };
};

# public symbols added in 3.2.2 (please keep in alphabetical order):
WXU_3.2.2 {
    extern "C++" {
        "wxUILocale::GetSystemLocaleId";
        "wxWithImages::GetImageLogicalSize";
        "wxWithImages::GetImageBitmapFor";
    };
};

# public symbols added in 3.2.1
WXU_3.2.1 {
    extern "C++" {
        "wxApp::GTKAllowDiagnosticsControl()";
        "wxFileDialog::AddShortcut(const wxString&, int)";
    };
};

# symbols available since the beginning of this branch are only given
# generic branch tag (don't remove this!):

WXU_3.2 {
        *;
};
